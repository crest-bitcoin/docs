---
title: "RFQ-T (Trader-Initiated Settlement)"
description: "Deep dive into user-initiated trade execution on Crest"
---

RFQ-T (Request for Quote - Trader) is Crest's **user-initiated settlement mechanism** where traders directly execute trades by sending transactions to the Settlement contract. This mode supports both native cBTC and ERC20 input tokens.

## Key Characteristics

<CardGroup cols={2}>
  <Card title="User-executed" icon="user-gear">
    Users directly sign and send settlement transactions
  </Card>
  <Card title="Gas Payment" icon="gas-pump">
    Users pay their own gas fees, ensuring no dependency on third-party relayers
  </Card>
  <Card title="Native cBTC Support" icon="bitcoin">
    Unique support for native cBTC as input token via `msg.value`
  </Card>
  <Card title="Single Signature" icon="signature">
    Only requires market maker's signature, simplifying the execution flow
  </Card>
</CardGroup>

## How RFQ-T Works

### 1. Quote Request & Response

```mermaid
sequenceDiagram
    participant User
    participant RFQ Engine
    participant MarketMaker
    participant Settlement

    User->>RFQ Engine: Request Quote
    RFQ Engine->>MarketMaker: Ping for Quote
    MarketMaker->>RFQ Engine: Signed Quote Response
    RFQ Engine->>User: Best Quote
    User->>Settlement: settleRFQT() + MM Signature
    Settlement->>Settlement: Execute Trade
```

### 2. Settlement Function Call

```solidity
function settleRFQT(
    QuoteParams calldata params,
    bytes calldata marketMakerSignature
) external payable nonReentrant {
    require(params.user == msg.sender, "Sender must be the user");
    _validateRFQT(params, marketMakerSignature);
    _executeRFQT(params);
}
```

## Trade Execution Scenarios

### Scenario 1: Native cBTC → ERC20 Token

**Example: Selling 1 cBTC ($100,000) for USD**

```mermaid
sequenceDiagram
    participant User
    participant Settlement
    participant WCBTC
    participant MarketMaker
    participant USD

    Note over User,USD: Scenario 1: Native cBTC → USD
    User->>Settlement: settleRFQT() + 1 cBTC
    Settlement->>WCBTC: deposit() 1 cBTC
    WCBTC->>Settlement: 1 WCBTC
    Settlement->>MarketMaker: 1 WCBTC
    Settlement-->>USD: transferFrom(mm, user)
    USD->>User: 99,700 USD
    Settlement-->>USD: transferFrom(mm, settlement)
    USD->>Settlement: 300 USD
```

**Execution Flow:**
1. User sends native cBTC with transaction
2. Settlement wraps cBTC to WCBTC and sends to MM
3. Settlement uses transferFrom to:
   - Move 99,700 USD from MM to user
   - Move 300 USD fee from MM to itself

### Scenario 2: ERC20 Token → Native cBTC

**Example: Buying 1 cBTC with USD**

```mermaid
sequenceDiagram
    participant User
    participant Settlement
    participant WCBTC
    participant MarketMaker
    participant USD

    Note over User,USD: Scenario 2: USD → Native cBTC
    User->>Settlement: Approve USD spending
    User->>Settlement: settleRFQT()
    Settlement-->>USD: transferFrom(user, mm)
    USD->>MarketMaker: 100,000 USD
    Settlement-->>WCBTC: transferFrom(mm, settlement)
    WCBTC->>Settlement: 1 WCBTC
    Settlement->>WCBTC: withdraw() 1 cBTC
    Settlement->>User: 0.997 cBTC
```

**Execution Flow:**
1. Settlement uses transferFrom to move USD to MM
2. Settlement gets WCBTC from MM, unwraps it:
   - Sends 0.997 cBTC to user
   - Keeps 0.003 cBTC as fee

### Scenario 3: ERC20 → ERC20 Token

**Example: USD → WCBTC**

```mermaid
sequenceDiagram
    participant User
    participant Settlement
    participant MarketMaker
    participant USD
    participant WCBTC

    Note over User,WCBTC: Scenario 3: USD → WCBTC
    User->>Settlement: Approve USD spending
    User->>Settlement: settleRFQT()
    Settlement-->>USD: transferFrom(user, mm)
    USD->>MarketMaker: 100,000 USD
    Settlement-->>WCBTC: transferFrom(mm, user)
    WCBTC->>User: 0.997 WCBTC
    Settlement-->>WCBTC: transferFrom(mm, settlement)
    WCBTC->>Settlement: 0.003 WCBTC
```

## Technical Implementation

### Validation Process

```solidity
function _validateRFQT(
    QuoteParams memory params,
    bytes calldata marketMakerSignature
) private {
    // Prevent replay attacks
    require(!executedQuotes[params.quoteId], "Quote already executed");

    // Check expiration
    require(block.timestamp <= params.expiry, "Quote expired");

    // Validate market maker signature
    bytes32 quoteHash = hashQuote(params);
    require(
        validateSignature(params.marketMaker, quoteHash, marketMakerSignature),
        "Invalid market maker signature"
    );

    // Mark as executed
    executedQuotes[params.quoteId] = true;
}
```

### Execution Logic

```solidity
function _executeRFQT(QuoteParams memory params) private {
    (uint256 feeAmount, uint256 userReceiveAmount) = _calculateFee(params.amountOut);

    // Handle input token
    if (params.tokenIn == NATIVE_TOKEN) {
        require(msg.value == params.amountIn, "Incorrect cBTC amount");
        wcbtc.deposit{value: params.amountIn}();
        IERC20(address(wcbtc)).safeTransfer(params.marketMaker, params.amountIn);
    } else {
        IERC20(params.tokenIn).safeTransferFrom(
            params.user,
            params.marketMaker,
            params.amountIn
        );
    }

    // Handle output token
    if (params.tokenOut == NATIVE_TOKEN) {
        IERC20(address(wcbtc)).safeTransferFrom(
            params.marketMaker,
            address(this),
            params.amountOut
        );
        wcbtc.withdraw(params.amountOut);
        (bool success, ) = payable(params.user).call{value: userReceiveAmount}("");
        require(success, "cBTC transfer to user failed");

        if (feeAmount > 0) {
            collectedFees[NATIVE_TOKEN] += feeAmount;
        }
    } else {
        IERC20(params.tokenOut).safeTransferFrom(
            params.marketMaker,
            params.user,
            userReceiveAmount
        );

        if (feeAmount > 0) {
            IERC20(params.tokenOut).safeTransferFrom(
                params.marketMaker,
                address(this),
                feeAmount
            );
            collectedFees[params.tokenOut] += feeAmount;
        }
    }

    emit RFQSettled(/* ... */, true); // isRFQT = true
}
```

## User Experience Considerations

### Pre-Execution Checks

Before executing RFQ-T trades, we verify:

<AccordionGroup>
  <Accordion title="1. Balance Check">
    **For Native cBTC:**
    - Verify wallet has sufficient cBTC balance for trade amount
    - Add extra buffer for gas fees
    - Example: For 1 cBTC trade ($100,000), ensure balance > 1.0001 cBTC

    **For ERC20 Tokens:**
    - Check token balance matches or exceeds trade amount
    - Example: For 100,000 USDC trade, verify USDC balance ≥ 100,000
  </Accordion>

  <Accordion title="2. Token Approval">
    **When trading ERC20 tokens:**
    - Check if Settlement contract has sufficient allowance
    - Current allowance should be ≥ trade amount
    - If insufficient:
      1. Call approve() on token contract
      2. Set allowance for Settlement contract address
      3. Wait for approval transaction to confirm

    **Note:** Not required for native cBTC trades
  </Accordion>

  <Accordion title="3. Quote Validation">
    **Expiration:**
    - Check current timestamp vs quote expiry
    - Quotes typically valid for 3-4 seconds
    - Request new quote if expired
  </Accordion>
</AccordionGroup>

### Error Handling

Common errors and their resolutions:

<AccordionGroup>
  <Accordion title="Insufficient Balance">
    **Error**: "Insufficient cBTC balance for trade + gas"

    **Resolution**:
    - Check total cBTC needed (trade amount + gas fees)
    - Ensure wallet has sufficient native cBTC
    - Consider reducing trade size
  </Accordion>

  <Accordion title="Quote Expired">
    **Error**: "Quote expired"

    **Resolution**:
    - Request a new quote from the RFQ system
    - Execute trades promptly after receiving quotes
    - Consider implementing quote refresh functionality
  </Accordion>

  <Accordion title="Insufficient Allowance">
    **Error**: "ERC20: insufficient allowance"

    **Resolution**:
    - Approve the Settlement contract to spend tokens
    - Use `approve()` or `increaseAllowance()`
    - Consider unlimited approval for frequent trading
  </Accordion>
</AccordionGroup>
